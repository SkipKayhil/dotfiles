# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

export DISPLAY=:0.0

export DOTFILES=$HOME/.dotfiles

export XDG_CONFIG_HOME=${XDG_CONFIG_HOME:-$HOME/.config}
export XDG_CACHE_HOME=${XDG_CACHE_HOME:-$HOME/.cache}
export XDG_DATA_HOME=${XDG_DATA_HOME:-$HOME/.local/share}

export EDITOR='vim'

# Load dotfile utility functions
source "$XDG_CONFIG_HOME/zsh/utils.sh"

bindkey -e

if [[ -a ~/.localrc ]]; then
  source_file ~/.localrc
fi

# Ensure the xdg data & cache directories exist
[[ ! -d "${XDG_DATA_HOME}/zsh" ]] && mkdir -p "${XDG_DATA_HOME}/zsh"
[[ ! -d "${XDG_CACHE_HOME}/zsh" ]] && mkdir -p "${XDG_CACHE_HOME}/zsh"

# all of our zsh files
typeset -U config_files
setopt extendedglob
config_files=($DOTFILES/^[opt]/*.zsh)
unsetopt extendedglob

# load everything
source_files_in "${config_files[@]}"

# Load and initialize the completion system ignoring insecure directories with a
# cache time of 20 hours, so it should almost always regenerate the first time a
# shell is opened each day.
autoload -U compinit
_zcompdump="$XDG_CACHE_HOME/zsh/zcompdump"
_comp_files=(${_zcompdump}(Nm-20))
if (($#_comp_files)); then
  compinit -i -C -d "$_zcompdump"
else
  echo "Regenerating compdump..."
  compinit -i -d "$_zcompdump"
fi

# https://github.com/holman/dotfiles/blob/master/zsh/completion.zsh
#
# matches case insensitive for lowercase
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# pasting with tabs doesn't perform completion
zstyle ':completion:*' insert-tab pending

source_file $DOTFILES/opt/powerlevel10k/powerlevel10k.zsh-theme
source_file $DOTFILES/zsh/plugins/p10k.zsh

source_file $DOTFILES/opt/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh
