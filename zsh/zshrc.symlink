# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

export DISPLAY=:0.0

# shortcut to the dotfiles path
export ZSH=$HOME/.dotfiles
export DOTFILES=$HOME/.dotfiles

bindkey -e

if [[ -a ~/.localrc ]]; then
  source ~/.localrc
fi

export XDG_CONFIG_HOME=${XDG_CONFIG_HOME:-$HOME/.config}
export XDG_CACHE_HOME=${XDG_CACHE_HOME:-$HOME/.cache}
export XDG_DATA_HOME=${XDG_DATA_HOME:-$HOME/.local/share}

# Ensure the xdg data & cache directories exist
[[ ! -d "${XDG_DATA_HOME}/zsh" ]] && mkdir -p "${XDG_DATA_HOME}/zsh"
[[ ! -d "${XDG_CACHE_HOME}/zsh" ]] && mkdir -p "${XDG_CACHE_HOME}/zsh"

# all of our zsh files
typeset -U config_files
setopt extendedglob
config_files=($ZSH/^[opt]/*.zsh)
unsetopt extendedglob

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# Load and initialize the completion system ignoring insecure directories with a
# cache time of 20 hours, so it should almost always regenerate the first time a
# shell is opened each day.
autoload -U compinit
_zcompdump="$XDG_CACHE_HOME/zsh/zcompdump"
_comp_files=(${_zcompdump}(Nm-20))
if (($#_comp_files)); then
  compinit -i -C -d "$_zcompdump"
else
  echo "Regenerating compdump..."
  compinit -i -d "$_zcompdump"
fi

source $DOTFILES/opt/powerlevel10k/powerlevel10k.zsh-theme
source $DOTFILES/zsh/plugins/p10k.zsh

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

source $ZSH/opt/fast-syntax-highlighting/fast-syntax-highlighting.plugin.zsh
